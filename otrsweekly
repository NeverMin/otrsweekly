#!/usr/bin/env perl
use Mojolicious::Lite;

use Path::Tiny;
use Text::MultiMarkdown qw/markdown/;
use Time::Piece;

plugin 'JSONConfig';

if ( app->config->{app}->{reverse_proxy} ) {
    $ENV{MOJO_REVERSE_PROXY} = 1;
}

app->secrets( [ app->config->{app}->{secret}  || 'laASSA2j1o11udk0582euf9u2$!"HASH{MD5}'] );
app->mode( app->config->{app}->{mode}    || 'development' );

any '/'         => \&start;
any '/archive'  => \&archive;
any '/latest'   => \&latest;
any '/:week'    => [ week => qr/[0-9]{4}-[0-9]{2}/ ]=> \&week;

sub start {
    my $c = shift;

    my ($current_week, $next_monday) = _calc_dates( $c );
    my $next_issue                   = $next_monday->dmy;

    $c->stash(
        next_issue   => $next_issue,
        current_week => $current_week,
    );

    $c->render(template => 'index');
}

sub latest {
    my $c = shift;

    my ($current_week) = _calc_dates( $c );

    my $week = _render_week( $c, $current_week );

    if ( !$week ) {
        start( $c );
        $c->res->code( 404 );
        return;
    }

    $c->stash( week_data => $week );
    $c->render(template => 'week');
}

sub _calc_dates {
    my $c = shift;

    my $now              = localtime;
    my $last_monday_days = ( $now->day_of_week || 7 ) - 1;
    my $seconds_diff     = $last_monday_days * 24 * 60 * 60;

    my $last_monday = $now - $seconds_diff;
    my $week        = _get_week_from_date( $last_monday );

    if ( -f path( $c->app->home . '/markdown/' . $week . '.md' )->stringify ) {
        $last_monday += 7 * 24 * 60 * 60;
    }

    my $current_week = _get_week_from_date( $last_monday - 7 * 24 * 60 * 60 );

    return ( $current_week, $last_monday );
}

sub _get_week_from_date {
    my ($date) = @_;

    my ($week, $month, $year) = (
        $date->week,
        $date->mon,
        $date->year,
    );

    if ( $week == 53 && $month == 1 ) {
        $year--;
    }
    elsif ( $week == 1 && $month == 12 ) {
        $year++;
    }

    return sprintf "%04d-%02d", $year, $week;
}

sub archive {
    my $c = shift;

    my $archive = path( $c->app->home . '/doc/archive.html' )->slurp_utf8();

    $c->stash( weeks => $archive );
    $c->render(template => 'archive');
}

sub week {
    my $c = shift;

    my $week = _render_week( $c, $c->param('week') );

    if ( !$week ) {
        start( $c );
        $c->res->code( 404 );
        return;
    }

    $c->stash( week_data => $week );
    $c->render(template => 'week');
}

sub _render_week {
    my ($self, $file) = @_;

    return if !$file;

    $file =~ s/[^0-9-]//g;

    my $dir = $self->param('draft') ? 'drafts' : 'markdown';

    my $path = $self->app->home . '/' . $dir . '/' . $file . '.md';
    if (! -f $path ) {
        return;
    }

    my $content = path( $path )->slurp_utf8;

    my ($meta,$data) = split /\n\n/, $content, 2;

    my ($date, $editor, $title) = $meta =~ m{
      ^Date: \s+ (.*) \s+
      ^Editor: \s+ (.*) \s+
      ^Title: \s+ (.*) $
    }xms;

    (my $id = $title) =~ s{[^A-Za-z0-9-]}{}g;
    my $html = markdown( $data );

    my %week = (
        date    => $date,
        editor  => $editor,
        title   => $title,
        id      => $id,
        path    => $file,
        html    => $html,
    );

    return \%week;
}


app->start;
__DATA__

@@ index.html.ep
% layout 'default';
% title 'OTRSWeekly - Start';

<h1>OTRSWeekly - the weekly newsletter about OTRS</h1>

<p>Next issue on <%= $next_issue %>.</p>

<p>Want to see the current newsletter? Check out the <a href="<%= url_for('/latest') %>">latest issue (<%= $current_week %>)</a>.</p>

@@ archive.html.ep
% layout 'default';
% title 'OTRSWeekly - Archive';

<h1>Archive of all OTRSWeekly editions:</h1>

%== $weeks

@@ week.html.ep
% layout 'default';
% title 'OTRSWeekly - Week ' . param( 'week' );
<h1><%= $week_data->{title} %></h1>
<div class="metadata"><%= $week_data->{date} %> // <%= $week_data->{editor} %></div>
%== $week_data->{html}

@@ layouts/default.html.ep
<!DOCTYPE html>
<html lang="de">
  <head>
    <meta charset="utf-8">
    <meta http-equiv="X-UA-Compatible" content="IE=edge">
    <meta name="viewport" content="width=device-width, initial-scale=1">
    <title>OTRSWeekly - news about OTRS</title>
    <meta name="description" content="">
    <meta name="revisit-after" content="30 days">
    <meta name="robots" content="index, follow">
    <meta name="keywords" content="OTRS, AddOns, Newsletter">
    
    <!-- Bootstrap -->
    <link href="/css/bootstrap.min.css" rel="stylesheet">
    <link href="/css/custom.css" rel="stylesheet">

    <!-- HTML5 shim and Respond.js for IE8 support of HTML5 elements and media queries -->
    <!-- WARNING: Respond.js doesn't work if you view the page via file:// -->
    <!--[if lt IE 9]>
      <script src="https://oss.maxcdn.com/html5shiv/3.7.2/html5shiv.min.js"></script>
      <script src="https://oss.maxcdn.com/respond/1.4.2/respond.min.js"></script>
    <![endif]-->

    <style type="text/css">
        .meta { color: #acacac; font-size: 0.7em }
    </style>
  </head>
  <body>

    <nav class="navbar navbar-inverse navbar-fixed-top">
      <div class="container">
        <div class="navbar-header">
          <button type="button" class="navbar-toggle collapsed" data-toggle="collapse" data-target="#navbar" aria-expanded="false" aria-controls="navbar">
            <span class="sr-only">Toggle navigation</span>
            <span class="icon-bar"></span>
            <span class="icon-bar"></span>
            <span class="icon-bar"></span>
          </button>
          <a class="navbar-brand" href="#">OTRS Weekly - weekly news around OTRS</a>
        </div>
        <div id="navbar" class="collapse navbar-collapse">
          <ul class="nav navbar-nav">
            <li class="active"><a href="/">Home</a></li>
            <li><a href="/latest">Latest</a></li>
            <li><a href="/archive">Archive</a></li>
          </ul>
        </div><!--/.nav-collapse -->
      </div>
    </nav>

    <div class="container">

      <div class="starter-template">
%= content
      </div>

    </div>

    <hr>

    <footer class="footer">
      <div class="inside">
        <p>
          <a style="padding-right:10px;" href="http://feature-addons.de/file/data_privacy">Datenschutz</a>
          <a href="http://feature-addons.de/file/imprint">Impressum</a>
        </p>
      </div>
    </footer>

    <script src="/js/jquery.min.js"></script>
    <script src="/js/bootstrap.min.js"></script>
    <!-- IE10-Anzeigefenster-Hack fÃ¼r Fehler auf Surface und Desktop-Windows-8 -->
    <script src="/js/ie10-viewport-bug-workaround.js"></script>
  </body>
</html>

